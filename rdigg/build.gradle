apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    displayName = 'rdigg'
}

sourceSets {
    test {
        groovy {
            srcDirs = ['src/test/common/groovy', 'src/test/unit/groovy', 'src/test/integration/groovy', 'src/test/ui/groovy']
        }
    }
}

buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
	}
}

repositories {
    mavenCentral()
}

shadowJar {
   baseName = displayName
   classifier = null
   version = null
}

jar {
    manifest {
        attributes 'Main-Class': 'org.fst.backup.Main'
    }
}

dependencies {
	compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'
	testCompile 'junit:junit:4.12'
}

task createBatch() << {
	String batchFileName = displayName + '.bat' 
	String command = 'start javaw -jar ' + displayName + '.jar'
	if (!libsDir.exists()) {
		libsDir.mkdirs()
	}
	File bat = new File(libsDir, batchFileName) << command
}

task copyToReadyShare(type: Exec) {
    commandLine 'xcopy', '/Y', '/S', '/I', 'C:\\dev\\repository\\backup\\rdigg\\build\\libs', '\\\\READYSHARE\\All\\Dev\\rdigg'
}
